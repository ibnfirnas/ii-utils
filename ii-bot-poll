#! /usr/bin/awk -f

BEGIN {
    me = me ? me : "pollbot"
}

$2 == admin && $3 == me && $4 == "create"  {create() ; next}
$2 != "-!-" && $3 == me && $4 == "vote"    {vote()   ; next}
$2 != "-!-" && $3 == me && $4 == "results" {results(); next}
$2 == admin && $3 == me && $4 == "destroy" {destroy(); next}

function create(    poll, i, opt) {
    poll = $5
    for (i=6; i<=NF; i++) {
        opt = $i
        Votes[poll, opt] = 0
    }
}

function vote(    nick, poll, opt, _voted) {
    nick = $2
    poll = $5
    opt = $6
    if (!Voted[poll, nick]++) Votes[poll, opt]++
}

function results(    poll, poll_requested, poll_opt0, poll_opt, opt) {
    poll_requested = $5
    for (poll_opt0 in Votes) {
        split(poll_opt0, poll_opt, SUBSEP)
        poll = poll_opt[1]
        opt  = poll_opt[2]
        if (poll == poll_requested) print opt, Votes[poll, opt]
    }
}

function destroy(    poll, poll_requested, poll_opt0, poll_nick0, poll_opt, poll_nick, opt, nick) {
    poll_requested = $5
    for (poll_opt0 in Votes) {
        split(poll_opt0, poll_opt, SUBSEP)
        poll = poll_opt[1]
        opt  = poll_opt[2]
        if (poll == poll_requested) delete Votes[poll, opt]
    }
    for (poll_nick0 in Voted) {
        split(poll_nick0, poll_nick, SUBSEP)
        poll = poll_nick[1]
        nick = poll_nick[2]
        if (poll == poll_requested) delete Voted[poll, nick]
    }
}
